buildscript {
    configurations.maybeCreate("pitest")
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.6'
        pitest 'org.pitest:pitest-junit5-plugin:0.12'
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'java'
    id "jacoco"
}

apply plugin: "info.solidsoft.pitest"
apply plugin: 'io.spring.dependency-management'

group 'com.github.mikesafonov'
version '1.0.0'

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.5.1'
}

bootJar {
    mainClassName = 'com.github.mikesafonov.prometheus.telegram.Application'
    archiveFileName = "prometheus-telegram-alert.jar"
    launchScript()
}

springBoot {
    buildInfo {
        properties {
            additional = ["version": project.version]
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    dependencies {
        dependencySet(group:'org.junit.jupiter', version: '5.6.1') {
            entry 'junit-jupiter-api'
            entry 'junit-jupiter-engine'
            entry 'junit-jupiter-params'
        }
        dependencySet(group:'org.junit.platform', version: '1.6.1') {
            entry 'junit-platform-engine'
            entry 'junit-platform-commons'
        }
    }
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'com.github.mikesafonov:spring-boot-starter-prometheus-alerts:1.0.0'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.apache.logging.log4j:log4j-web'

    implementation 'com.vdurmont:emoji-java:5.1.1'

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation("org.junit.jupiter:junit-jupiter:5.6.1")
    testImplementation("org.mockito:mockito-core")
}

jacoco {
    toolVersion = "0.8.3"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport, "pitest" 
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

pitest {
    testPlugin = "junit5"
    targetClasses = ['com.github.mikesafonov.prometheus.telegram.dto.*', 'com.github.mikesafonov.prometheus.telegram.service.*']
    excludedClasses = ['com.github.mikesafonov.prometheus.telegram.config.*', 'com.github.mikesafonov.prometheus.telegram.controllers.*']
    threads = 4
    outputFormats = ['HTML', 'XML']
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}