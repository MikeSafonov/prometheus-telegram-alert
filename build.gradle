plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'java'
    id "jacoco"
    id 'info.solidsoft.pitest' version '1.5.0'
}

apply plugin: 'io.spring.dependency-management'

group 'com.github.mikesafonov'
version '1.1.0'

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.5.1'
}

bootJar {
    mainClassName = 'com.github.mikesafonov.prometheus.telegram.Application'
    archiveFileName = "prometheus-telegram-alert.jar"
    launchScript()
}

springBoot {
    buildInfo {
        properties {
            additional = ["version": project.version]
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR4"
    }
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation("org.springframework.cloud:spring-cloud-starter-vault-config")
    implementation 'com.github.mikesafonov:spring-boot-starter-prometheus-alerts:1.0.0'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.apache.logging.log4j:log4j-web'

    implementation 'com.vdurmont:emoji-java:5.1.1'

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
        exclude module: "junit-vintage-engine"
    }
    testImplementation("org.mockito:mockito-core")
}

jacoco {
    toolVersion = "0.8.3"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport, "pitest" 
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ['com.github.mikesafonov.prometheus.telegram.dto.*', 'com.github.mikesafonov.prometheus.telegram.service.*']
    excludedClasses = ['com.github.mikesafonov.prometheus.telegram.config.*', 'com.github.mikesafonov.prometheus.telegram.controllers.*']
    threads = 4
    outputFormats = ['HTML', 'XML']
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}