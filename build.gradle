buildscript {
    configurations.maybeCreate("pitest")
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
        pitest 'org.pitest:pitest-junit5-plugin:0.9'
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
    id "jacoco"
}

apply plugin: "info.solidsoft.pitest"
apply plugin: 'io.spring.dependency-management'

group 'com.github.mikesafonov'
version '1.0.0'

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.5.1'
}

bootJar {
    mainClassName = 'com.github.mikesafonov.prometheus.telegram.Application'
    archiveFileName = "prometheus-telegram-alert.jar"
    launchScript()
}

springBoot {
    buildInfo {
        properties {
            additional = ["version": project.version]
        }
    }
}

repositories {
    mavenCentral()
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.apache.logging.log4j:log4j-web'

    implementation 'com.vdurmont:emoji-java:4.0.0'

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    def junitBomVersion = '5.5.1'

    testImplementation(platform("org.junit:junit-bom:$junitBomVersion"))
    
    testImplementation("org.junit.jupiter:junit-jupiter")
}

jacoco {
    toolVersion = "0.8.3"
}

test {
    ignoreFailures = true
    useJUnitPlatform()
    finalizedBy jacocoTestReport, "pitest" 
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

pitest {
    testPlugin = "junit5"
    targetClasses = ['com.github.mikesafonov.prometheus.telegram.*']
    threads = 4
    outputFormats = ['HTML', 'XML']
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}